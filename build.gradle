plugins {
    id 'org.springframework.boot' version '2.6.4'
    id 'application'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

application {
    mainClass = 'Application'
}

group 'nsu.gaiduk'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "${projectDir}/src/main/resources/xsd/OSMSchema.xsd"
    outputs.dir classesDir

    doLast() {
        project.ant {
            // Create output directories
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            taskdef name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath

            xjc(destdir: sourcesDir, schema: schema) {
                produces(dir: sourcesDir, includes: '**/*.java')
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: 'lines,vars,source',
                    includeantruntime: false,
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: '**/*.java')
                include(name: '*.java')
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: '**/*.java')
                }
            }
        }
    }
}

dependencies {
    jaxb (
            'com.sun.xml.bind:jaxb-xjc:3.0.2'
    )
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.2'
    implementation 'com.sun.xml.bind:jaxb-core:3.0.2'
    implementation 'com.sun.xml.bind:jaxb-osgi:3.0.1'

    implementation(files(genJaxb.classesDir).builtBy(genJaxb))

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor "org.projectlombok:lombok:1.18.22"
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0-RC3'
    implementation 'jakarta.activation:jakarta.activation-api:2.1.0'
    implementation 'org.postgresql:postgresql:42.3.3'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    compileOnly 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
}

test {
    useJUnitPlatform()
}